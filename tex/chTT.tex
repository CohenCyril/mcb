\Chapter{Type Theory}{The Curry-Howard correspondence}
\label{ch:ttch}

The authors made the deliberate choice to postpone the presentation of
the mathematical foundations of the \Coq{} system and the formal
definition of its language. Instead, the previous chapters have
insisted on the definition of (typed) programs and on the way these
programs can be used to decribe computable functions and decidable
predicates. This take on calculations is indeed both at the core of
the type theory underlying \Coq{} and one of the crucial ingredients
to the methodology basing the \mcbMC{} library. The present chapter is
devoted to a more in-depth presentation of these mathematical
presentation, alhough an exhaustive description shall remain out of
the scope of the present book. The interested reader can refer to more
specialized references like~\cite{hottbook} or \cite{ttfp}, or the
shorter survey in the Handbook of Automated
Reasoning~\cite[Volume 2, Chapter 18]{handbook-ar}.

.



% By now the informed reader is likely to wonder when the authors of the book
% will eventually mention the mathematical foundation of \Coq{},
% % a variant of type theory called \mcbCIC{},
% and
% in particular explain the Curry-Howard isomorphism.  Indeed this text departs
% from the standard presentation of \Coq{}, that typically starts by
% explaining the logic and showing how standard connectives like
% conjunction or disjunction are defined in term of inductive types.

% We made a deliberate choice to put forward the programming aspects of
% type theory and how programs can be used both to express computable functions
% and decidable predicates or boolean connectives.
% Indeed this formalization choice is one of the main ingredients of the \mcbMC{}
% library.  The computational behavior of programs is at the core of
% type theory.  The fact that a function computes to a value requires no
% proof: 5 and 2+3 are indistinguishable from a logical standpoint.
% This fact provided a powerful form of automation, and as we will
% see later will also ease the construction of sub types.

% It is finally time to provide a minimal presentation of the statements as types
% correspondence.  The universe of boolean predicates is limited: one
% cannot for example express a general $\exists$ quantification, and also the
% status of the $\forall$ quantification has been so far never really explained.
% This presentation is far from being exhaustive, the interested reader can find
% a modern presentation of type theory in \cite{hottbook}.

\section{Connectives}

\subsection{Primitive types and terms formers}\label{sec:chi}

In type theory we say that \emph{logical statements} correspond to
\emph{types} and that \emph{proofs} correspond to \emph{programs}.

The simplest example one could think of is the formal statement saying that
a proposition $A$ implies itself, namely $A \rightarrow A$.  If one reads
the implication symbol $\to$ as the type of functions, the statements reads
as a function from $A$ to $A$.  A program with that type would be
\C{(fun x : A => x)}.  Such program takes in input a term $x$ of
type $A$, that we here see as a proof of $A$, and returns it, i.e. it
produces in output a proof of $A$.  We say that such program is a
proof of $A \rightarrow A$.

What is striking here is that the same class of terms represents both programs
and proofs, for example the identity function of natural numbers has the very
same shape of the proof we've just seen.  So we are left with only two
concepts, types and terms, playing a double role.  We now see how types and
terms can be formed.

Regarding types, the primitive type formers we have seen are the function space
$\to$, also logical implication, and the universal quantification
$\forall$, used to describe polymorphic function as well as parametric lemmas
(like induction principles).
Standard logical rules describing how one can prove formulas involving these
connectives are:

\begin{center}
\AxiomC{$A$}
\noLine
\UnaryInfC{$\vdots$}
\noLine
\UnaryInfC{$B$}
\RightLabel{$\to_I$}
\UnaryInfC{$A \to B$}
\DisplayProof
\hspace{1cm}
\AxiomC{$B$}
\RightLabel{$\forall_I$ ($x$ fresh)}
\UnaryInfC{$\forall x, B$}
\DisplayProof
\end{center}

The former reads: to prove $A \to B$ one can prove $B$ under the
assumption $A$.  The latter: to prove $\forall x,B$ one can prove $B$
assuming that $x$ is fresh.  We annotate such rules with the terms
corresponding to these proof rules.

\begin{center}
\AxiomC{\C{x : }$~A$}
\noLine
\UnaryInfC{$\vdots$}
\noLine
\UnaryInfC{\C{b : }$~B$}
\RightLabel{$\to_I$}
\UnaryInfC{\C{(fun x : A => b) : }$~A \to B$}
\DisplayProof
\hspace{1cm}
\AxiomC{\C{b : }$~B$}
\RightLabel{$\forall_I$}
\UnaryInfC{\C{(fun x : A => b) : }$~\forall x, B$}
\DisplayProof
\end{center}

The anonymous function constructor \C{(fun .. => ..)} serves as a proof
for both rules: the term \C{b} is in both cases a proof of $B$.  The only
difference is that $x$ is only allowed to occur in $B$ in the second rule.
Said otherwise in type theory $\to$ is a special case of $\forall$ where
the bound variable does not occur in the quantified formula.
For example
there is no semantic difference between \C{(nat -> bool)} and
\C{(forall x : nat, bool)}.

Logical connectives come with elimination rules, in particular

\begin{center}
\AxiomC{$A \to B$}
\AxiomC{$A$}
\RightLabel{$\to_E$}
\BinaryInfC{$B$}
\DisplayProof
\hspace{1cm}
\AxiomC{$\forall x, B$}
\RightLabel{$\forall_E$ ($t$ a term)}
\UnaryInfC{$B[t/x]$}
\DisplayProof
\end{center}

Function application serves as a proof for both rules.

\begin{center}
\AxiomC{\C{f : }$~A \to B$}
\AxiomC{\C{t : }$~A$}
\RightLabel{$\to_E$}
\BinaryInfC{\C{(f t) :}$~B$}
\DisplayProof
\hspace{1cm}
\AxiomC{\C{f : }$~\forall x : A, B$}
\AxiomC{\C{t : }$~A$}
\RightLabel{$\forall_E$}
\BinaryInfC{\C{(f t) : }$~B[t/x]$}
\DisplayProof
\end{center}

The programs as proofs correspondence has a visible impact in the proofs
part of the \mcbMC{} library.  In particular quantified lemmas, being programs,
can be instantiated by simply passing arguments to them.  Exactly as one can
pass \C{3} to \C{addn} and obtain \C{(addn 3)}, the function adding three, one
can ``pass'' \C{3} to the lemma \C{addnC} and obtain a proof of the statement
\C{(forall y, 3 + y = y + 3)}.  Remark that the argument passed to \C{addnC}
shows up in the type of the resulting term \C{(addnC 3)}:  the type of the
\C{addnC} program depends on the value the program is applied to.  That is the
difference between the \emph{dependent function space} ($\forall$)
and the standard function space ($\to$).
\index[concept]{dependent function space}

\mantra{Lemma names can be used as functions and you can pass to them arguments.
For example
\C{(addnC 3)} is a proof that \C{(forall y, 3 + y = y + 3)} and
\C{(prime_gt0 p_pr)} is a proof that \C{(0 < p)} whenever
\C{(p_pr : prime p)}.}

Finally note that the $\forall$ quantification specifies the type of the bound
variable, and that the $\forall_E$ checks the term $t$ has the ``right type'',
otherwise we get a type error.  What is particularly relevant to the \mcbMC{}
library is the notion of right type, or how types are compared.  Indeed
the $\to_E$ rule (and also the $\forall_E$) should be written as follows.

\begin{center}
\AxiomC{\C{f : }$~A \to B$}
\AxiomC{\C{a : }$~A'$}
\AxiomC{$A \equiv A'$}
\RightLabel{$\to_E$}
\TrinaryInfC{\C{(f a) :}$~B$}
\DisplayProof
\end{center}

We say that $A$ and $A'$ are checked for \emph{convertibility} ($\equiv$),
i.e. if they are equal up to computation.  As a consequence
the term \C{(isT : true)} is a perfectly valid proof of
\C{(0 < n.+1)}, as well as \C{(odd 7)} or \C{(prime 27)}
and can be passed to a lemma expecting any of these.
\index[coq]{\C{isT}}
\index[concept]{convertibility}

\subsection{Inductive types}\label{ssec:indtypes}

In the previous chapters we used many data types already.  For example we have
seen the type \C{nat} and its constructors \C{O} and \C{S}.  Through the looking
glasses of the Curry-Howard correspondence \C{O}, being a term of type \C{nat}
can represent a ``proof'' of \C{nat}.  For data types we prefer to say
\emph{inhabited} rather than \emph{proved}, but there is no real difference.

Inductive ``data'' types can be used to represent logical connectives, in
particular the ones we miss so far, like the existential quantifier.

\begin{center}
\AxiomC{$A$} \AxiomC{$B$}
\RightLabel{$\wedge_I$}
\BinaryInfC{$A \wedge B$}
\DisplayProof
\hspace{1cm}
\AxiomC{$A \wedge B$}
\RightLabel{$\wedge_E$ (left)}
\UnaryInfC{$A$}
\DisplayProof
\end{center}

The first rule reads: to prove $A \wedge B$ one needs to prove both
$A$ and $B$.  The latter lets one prove $A$ whenever one can prove
the stronger statement $A \wedge B$.

In \Coq{} such connective can be characterized by the following
inductive definition.

\begin{coq}{name=And}{}
Inductive and (A B : Prop) : Prop := conj (pa : A) (pb : B).
Notation "A /\ B" := (and A B).
\end{coq}
\index[vernac]{\C{Inductive}}

Remark that the ``data'' type \C{and} is tagged as \C{Prop}, i.e.  we declare
the intention to use it as a logical connective rather than a data type.  The
only constructor \C{conj} that can be used to inhabit \C{and} takes two
arguments, a proof of \C{A} and a proof of \C{B}, faithfully modelling the
logical rule \C{$\wedge_I$}.  Note that \C{A} and \C{B} are parameters of the
inductive definition, similarly to the definition of polymorphic lists
or pairs.

It is worth to note that the definition of the pair data type,
Exercise~\ref{ex:pair}, is almost identical to the one of \C{and}.

\begin{coq}{}{}
Inductive prod (A B : Type) := pair (a : A) (b : B).
\end{coq}
The striking similarity witnesses how programs (and data) are used in \Coq{}
to represent proofs.

Pattern matching provides a way to express the elimination rule for
conjunction as follows:

\begin{coq}{name=Ande1}{}
Definition proj1 A B (p : A /\ B) : A :=
  match p with conj a _ => a end.
\end{coq}
\index[coq]{\C{conj}}

Now recall the similarity between $\to$ and $\forall$, where the former is the
simple, non dependent, case of the latter.  If we check the type of
the \C{conj} constructor

\begin{coq}{name=Ande1}{width=4cm}
About conj.
\end{coq}
\begin{coqout}{}{width=8cm}
conj: forall A B : Prop, A -> B -> A /\ B
\end{coqout}
we may wonder what happens if the type of the second argument (i.e. \C{B}) is
made dependent on the value of the first argument (of type \C{A}).
What we obtain is the inductive definition corresponding to the
existential quantification.

\begin{coq}{name=Ande1}{}
Inductive ex (A : Type) (P : A -> Prop) : Prop :=
  ex_intro (x : A) (p : P x).
Notation "'exists' x : A , p" := (ex A (fun x : A => p)).
\end{coq}
\index[coq]{\C{ex_intro}}

The \C{ex\_intro} constructor is the only mean to prove a statement like
\C{(exists n, prime n)}.  In such case the first argument would be a number
\C{n} of type \C{nat} while the second argument would be a proof \C{p} of type
\C{(prime n)}.  The \C{ex} inductive definition is again pretty similar to the
pair, but note that the type of the second component depends on the value of
the first one.  Last note the parameter \C{P} that is a function
representing an arbitrary predicate over a term of
type \C{A}.  Hence \C{(P a)} is the instance of the predicate to \C{a}.  E.g.
the predicate of being an even prime number is expressed as
\C{(fun x : nat => ~~ (odd x) && prime x)}, and the statement expressing the
existence of such number is
\C{(ex nat (fun x : nat => ~~ (odd x) && prime x))}.

We quickly see the inductive definition of the disjunction \C{or}
and its two constructors \C{or\_introl} and \C{or\_intror}.

\begin{coq}{name=Or}{}
Inductive or (A B : Prop) : Prop := or_introl (a : A) | or_intror (b : B).
Notation "A \/ B" := (or A B).
\end{coq}

The elimination rule can again be expressed by pattern matching:

\begin{coq}{name=Or}{}
Definition or_ind (A B P : Prop)
  (aob : A \/ B) (pa : A -> P) (pb : B -> P) : P :=
  match aob with or_introl a => pa a | or_intror b => pb b end.
\end{coq}
\index[coq]{\C{or_introl}}
\index[coq]{\C{or_intror}}

The detail worth noting here is that the pattern match construct has two
branches, and each branch represents a distinct sub proof.  In this
case to prove \C{P} starting from \C{A \\/ B} one has to deal with all
cases: prove \C{P} under the assumption \C{A} and prove \C{P}
under the assumption \C{B}.

Usual constants and connectives as $\top$, $\bot$ and $\neg$
can be defined as follows.

\begin{coq}{name=TrueFalse}{}
Inductive True : Prop := I.
Inductive False : Prop := .
Definition not (A : Prop) := A -> False.
Notation "~ A" := (not A).
\end{coq}
\index[coq]{\C{True}}
\index[coq]{\C{False}}
\index[coq]{\C{I}}
\index[coq]{\C{not}}

Remark that to prove \C{True} one has simply to provide \C{I} that has no
arguments.  So proving \C{True} is trivial, and as a consequence eliminating it
provides little help (i.e. no extra knowledge is obtained by pattern matching
over \C{I}).  Conversely it is impossible to prove \C{False}, since it has no
constructor, and pattern matching on \C{False} can inhabit any type, since no
branch has to be provided.

\begin{coq}{name=exfalso}{}
Definition exfalso (P : Prop) (f : False) : P :=
  match f with end.  (* no constructors, no branches *)
\end{coq}

The only base predicate we are left to describe is equality.  The reason we
left it as the last one is that it has a tricky nature.  In particular
equality, as we have seen in the previous chapters, is an open notion
in the following sense.  Terms that compute to the same syntactic expression
are considered as equal, and this is true for any program the user may write.
Hence such notion of equality needs to be somewhat primitive, as
\C{match} and \C{fun} are.  One also expects such notion to come
with a substitutivity property: replacing equals by equals must be licit.

The way this internal notion is exposed is via the concept of index
on which an inductive family may vary.

\begin{coq}{name=Eq}{}
Inductive eq (A:Type) (x:A) : A -> Prop := erefl : eq A x x.
Notation "x = y" := (@eq _ x y).
\end{coq}
\index[concept]{equality}
\index[coq]{\C{erefl}}

This is the first time we see a function type after the \C{:} symbol
in an inductive type declaration.
The \C{eq} type constructor takes three arguments: a type \C{A} and
two terms of that type (the former is named \C{x}).
Hence one can write \C{(a = b)} whenever \C{a} and \C{b}
have the same type.
The \C{erefl} constructor takes no arguments, as \C{I}, but its type
annotation says it can be used to inhabit only the type \C{(x = x)}.
Hence one is able to prove \C{(a = b)} only when \C{a} and \C{b} are
equal up to computation (as in indistinguishable from a logical standpoint).
Conversely by eliminating a term
of type \C{(a = b)} one discovers that  \C{a} and \C{b} are
equal and \C{b} can be freely replaced by \C{a}.

\begin{coq}{name=EqInd}{}
Definition eq_ind A (P : A -> Prop) x (px : P x) y (e : x = y) : P y :=
  match e with erefl => px end.
\end{coq}

The notion of equality is one of the most intricate aspects of type
theory and an in depth study of it is out of the scope of this book.  The interested reader
finds an extensive study of this subject in~\cite{hottbook}.  Later in this
chapter we define and use other inductive families to take advantage
of the ``automatic'' substitution of the implicit equations we see here:
while \C{px} has type \C{(P x)} it is accepted as an
inhabitant of \C{(P y)} because inside the \C{match} the term \C{y}
is automatically replaced by \C{x}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{Proof commands}

Since proofs are just terms one could,
in principle, use no proof language and input directly proof terms.
Indeed this was the modus operandi in the pioneering work of
De Bruijn on Automath (automating mathematics) in the seventies~\cite{nederpelt-94}.
Of course a dedicated proof language can relief the user from
many tedious details.  The proof commands we have seen so far can all be
explained in terms of the proof terms they produce behind the scenes.
For example \C{case: n} provides a much more compact syntax
for \C{(match .. with .. end)} and it produces a
\C{match} expression with the right
shape by looking at the type of \C{n}.  If \C{n} is a natural number then there
are two branches, the one for the \C{S} constructor carries an argument of type
\C{nat}, the other one is for \C{0} and binds no additional term.
The \C{case:} tactic is general enough to work with any inductive data type
and inductive predicate.

The \C{apply:} tactic generates an application.  For example \C{apply: addnC}
generates the term \C{(addnC t1 t2)} by figuring out the correct values of
\C{t1} and \C{t2}, or opening new goals when this cannot be done, i.e.
if the lemma takes in input proofs, like \C{contraL}.

There is a list of proof commands that are shorthands for \C{apply:}
and is only worth mentioning here briefly. \C{split} proves a conjunction
by applying the \C{conj} constructor, \C{left} and \C{right} prove a
disjunction by applying \C{or\_introl} and \C{or\_intror} respectively.
\C{exist t} proves an existentially quantified formula by providing
the witness \C{t} and, later, a proof that \C{t} validates the predicate.
Finally \C{reflexivity} proves an equality by applying \C{erefl}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Managing the proof context}
The only primitive constructor that remains without an associated proof command
is \C{(fun .. => ..)}.  Operationally what the $\to_I$ and
$\forall_I$ logical rule do is to introduce into the proof context a
new entry.  So far we either expressed this step at the beginning of proofs
by putting such items just after the name of the lemma being prover, or
just after a \C{case:} or \C{elim:} proof command.  The current section
expands on this subject covering the full management of the proof context.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\subsection{The stack model}
\index[concept]{goal stack model}

The presentation we gave so far of proof commands like \C{case: n => [|m]}
is oversimplified.  While \C{case} is indeed the proof command in
charge of performing case analysis the ``\C{: n}'' and ``\C{=> [|m]}''
parts are decorators to prepare the goal and post process the result of
the proof command.  These decorators deal with what we typically call
\emph{bookkeeping}: actions that are necessary in order to obtain readable and
robust proof scripts but that are too frequent to benefit from a move verbose
syntax.  Bookkeeping actions do convey a lot of information, like where
names are given to assumptions, but also let one deal with annoying details
using a compact, symbolic, language.  Note that all bookkeeping actions
correspond to regular, named, proof commands.  It is the use one makes of them
that may be twofold: a case analysis in the middle of a proof may start two
distinct lines of reasoning, and hence it is worth being noted explicitly with
the \C{case} word; conversely de-structuring a pair to obtain the two
components can hardly be a relevant step in a proof, so one may prefer to
perform such bookkeeping action with a symbolic, compact, notation
corresponding to the same \C{case} functionality.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\mcbLEVEL{1}
\mcbsubsubsection{Pulling from the stack}
\index[ssr]{\C{tactic => ..}}

Lets start with the post processing phase, called \emph{introduction pattern}.
The postfix ``\C{=> ...}'' syntax can be used in conjunction with any proof
command, and it performs a sequence of actions on the first goal assumption or
quantified variable.  With these looking glasses, the goal becomes a
\emph{stack}. Take for example this goal:

\begin{coqout}{name=Stack}{}
========================
forall xy, prime xy.1 -> odd xy.2 -> 2 < xy.2 + xy.1
\end{coqout}

Before accessing the assumption \C{(prime xy.1)} one has to name the
bound variable \C{xy}, exactly as one can only access a stack from its top.
The execution of \C{=> xy pr\_x odd\_y} is just the composition of
\C{=> xy} with \C{=> pr\_x} and finally \C{=> odd\_y}.  Each action
pulls out of the stack an item and names it.  The \C{move} proof
command does nothing,  we use it as a placeholder
for the postfix \C{=>} bookkeeping action.

\begin{coq}{}{width=7cm}
move=> xy pr_x odd_y
\end{coq}
\begin{coqout}{name=Stack1}{width=5cm}
 xy : nat * nat
 pr_x : prime xy.1
 odd_y : odd xy.2
========================
 2 < xy.2 + xy.1
\end{coqout}

Now, en passant, one would like to decompose \C{xy} into its first
and second component.  Instead of the verbose \C{=> xy; case: xy => x y}
one can use the symbolic notation \C{[]} to perform such action.

\begin{coq}{}{width=7cm}
move=> [x y] pr_x odd_y
\end{coq}
\begin{coqout}{name=Stack2}{width=5cm}
 x, y : nat
 pr_x : prime (x,y).1
 odd_y : odd (x,y).2
========================
 2 < (x,y).2 + (x,y).1
\end{coqout}
\index[ssr]{\C{tactic => ..}!\C{=> [ .. $\mid$ .. ]} (case)}

One can place the \C{/=} switch to force the system to reduce the formulas on
the stack, before introducing them in the context, and obtain:

\begin{coq}{}{width=7cm}
move=> [x y] /= pr_x odd_y
\end{coq}
\begin{coqout}{name=Stack2}{width=5cm}
 x, y : nat
 pr_x : prime x
 odd_y : odd y
========================
 2 < y + x
\end{coqout}

One can also process an assumption trough a lemma.  For example
\C{prime\_gt1} states \C{(prime p -> 1 < p)} for any \C{p}, and we can
use it as a function to obtain a proof of \C{(1 < x)}  from a proof
of \C{(prime x)}.

\begin{coq}{}{width=7cm}
move=> [x y] /= /prime_gt1-x_gt1 odd_y
\end{coq}
\begin{coqout}{name=Stack2}{width=5cm}
 x, y : nat
 x_gt1 : 1 < x
 odd_y : odd y
========================
 2 < y + x
\end{coqout}
\index[ssr]{\C{tactic => ..}!\C{=> /=} (simplification)}

The leading \C{/} makes \C{prime\_gt1} do as a function instead of
as a name to be assigned to the top of the stack.  The \C{-} has no effect but
to visually link the function and name assigned to its output.
Indeed \C{-} can be omitted.

One could also examine \C{y}: it can't be \C{0}, since it would contradict
the assumption saying that \C{y} is \C{odd}.

\begin{coq}{}{width=7cm}
move=> [x [//|y]] /= /prime_gt1-x_gt1.
\end{coq}
\begin{coqout}{name=Stack2}{width=5cm}
 x, y : nat
 x_gt1 : 1 < x
 ========================
 ~~ odd y -> 2 < y.+1 + x
\end{coqout}
\index[ssr]{\C{tactic => ..}!\C{=> /view} (view application)}
\index[ssr]{\C{tactic => ..}!\C{=> //} (close trivial goals)}

This time the destruction of \C{y} generates to cases, hence the \C{[ .. | .. ]}
syntax mentioning the two branches.  In the first one, when \C{y} is \C{0},
the \C{//} action solves the goal, by the same trivial means
of the \C{by []} terminator.  In the second branch we name \C{y} the
new variable.

Now, the fact that \C{y} is even is not needed to conclude, so we can discard it using the \C{\_} dummy name.

\begin{coq}{}{}
by move=> [x [//|y]] /= /prime_gt1-x_gt1 _; apply: ltn_addl x_gt1.
\end{coq}

The way to dispose an already named assumption is to mention
its name in curly braces, as \C{=> \{x_gt1\}}.
\index[ssr]{\C{tactic => ..}!\C{=> \{name\}} (disposal)}

We finally conclude with the \C{apply:} command that here we use
with two arguments: a function and its last argument.

\begin{coq}{}{width=4cm}
About ltn_addl.
\end{coq}
\begin{coqout}{}{width=8cm}
ltn_addl : forall m n p : nat, m < n -> m < p + n
\end{coqout}

\C{apply:} will fill in the blanks between the function (the lemma name)
and the argument provided.  Note that by passing \C{x\_gt1}, the
variable \C{m} picks the value \C{1}.  The conclusion of \C{ltn\_addl}
hence unifies with \C{(2 < y.+1 + x)} because both \C{+} and \C{<} are
defined as programs that compute: addition exposes a \C{.+1} by
reducing to \C{2 < (y+x).+1}, then \C{<}, or better the underlying
\C{<=}, eats a successor from both sides, leading to \C{1 < y + x}
that looks like the conclusion of the lemma we apply.

Here we have shown all possible actions one can perform in an intro
pattern, squeezing the entire proof into a single line.  This has
to be seen both as an opportunity and as a danger: one can easily
make a proof unreadable by performing too many actions in the bookkeeping
operator \C{=>}.  At the same time a trivial sub-proof like this one
should take no more than a line, and in that case one typically
sacrifices readability in favor of compactness: what would you learn by
reading a trivial proof?  Of course,
finding the right balance only comes with experience.

\gotcha{The case intro pattern \C{[..|..]} obeys an exception: when it is the first
item of an intro pattern, it does not perform a case analysis, but only branch
on the subgoals.%\footnote{not the status quo, but I prefer this exception to the current one}
Indeed in \C{case: n => [|m]} only one case analysis is performed.}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\mcbLEVEL{1}
\mcbsubsubsection{Working on the stack}

The stack can also be used as a work place.  Indeed there is
no need to pull from the stack all items.  If we take the previous example

\begin{coqout}{name=Stack}{}
========================
forall xy, prime xy.1 -> odd xy.2 -> 2 < xy.2 + xy.1
\end{coqout}
and we stop just after applying the view, we end up is a valid state.

\begin{coq}{}{width=6cm}
move=> [x y] /= /prime_gt1.
\end{coq}
\begin{coqout}{name=Stack2}{width=6cm}
 x, y : nat
 ========================
 1 < x -> odd y -> 2 < y + x
\end{coqout}

One can also chain multiple views on the same stack item.

\begin{coq}{}{width=6cm}
move=> [x y] /= /prime_gt1/ltnW.
\end{coq}
\begin{coqout}{name=Stack2}{width=6cm}
 x, y : nat
 ========================
 0 < x -> odd y -> 2 < y + x
\end{coqout}
\index[ssr]{\C{tactic => ..}!\C{=> /view/view} (many views)}

Two other operations are available on the top stack item: specialization
and substitution.  Let's take the following conjecture.

\begin{coqout}{}{}
========================
(forall n, n * 2 = n + n) -> 6 = 3 + 3
\end{coqout}

The top stack item is a quantified assumption.  To specialize it to, say,
\C{3} one can write as follows:

\begin{coq}{}{width=4cm}
move=> /(_ 3).
\end{coq}
\begin{coqout}{}{width=8cm}
========================
3 * 2 = 3 + 3 -> 6 = 3 + 3
\end{coqout}
\index[ssr]{\C{tactic => ..}!\C{=> /(_ arg)} (specialization)}
The idea behind the notation is that when we apply a view to the top
stack item (we name it \C{top} here), as in \C{/v}, we are
forming the term \C{(v top)}, while
when we specialize the top assumption as in \C{/(_ x)} we form the
term \C{(top x)}.  The \C{_} is a place holder for the top item, and is
omitted in \C{/(view _)}.

When the top stack item is an equation one can substitute it using \C{<-}
and \C{->} for right-to-left and left-to-right respectively.

\begin{coq}{}{width=4cm}
move=> /(_ 3) <-.
\end{coq}
\begin{coqout}{}{width=8cm}
========================
6 = 3 * 2
\end{coqout}
\index[ssr]{\C{tactic => ..}!\C{=> ->} (rewriting L2R)}
\index[ssr]{\C{tactic => ..}!\C{=> <-} (rewriting R2L)}
In other words, the arrows are just a compact syntax for rewriting,
as in the \C{rewrite} tactic, with the top assumption.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\mcbLEVEL{1}
\mcbsubsubsection{Pushing to the stack}
\index[ssr]{\C{tactic : ..}}

We have seen how to pull items from the stack to the context.
Now let's see the so called \emph{discharging} operator \C{:}, performing
the converse operation.
Such operator decorates
proof commands as \C{move}, \C{case} and \C{elim}
 with actions to be performed before the command is actually run.

\gotcha{The colon symbol in \C{apply:} is not the discharging operator.
It is just a marker to distinguish the \C{apply:} tactic of
Ssreflect from the \C{apply} tactic of \Coq{}.
Indeed the two tactics, while
playing similar roles, behave very differently.}

Imagine we want to perform case analysis on \C{y} at this stage

\begin{coqout}{name=Stack2}{}
 x, y : nat
 x_gt1 : 1 < x
 odd_y : odd y
 ========================
 2 < y + x
\end{coqout}

The command \C{case: y} is equivalent to
\C{move: y; case.} where \C{move} once again is a place holder,
\C{: y} pushes onto the stack the \C{y} variable and \C{case}
operates on the top item of the stack.
%Pushing items on the stack is called \emph{discharging}.

Just before running \C{case} the goal looks like this:

\begin{coqout}{name=Stack2}{}
 x : nat
 x_gt1 : 1 < x
 odd_y : odd y
 ========================
 forall y, 2 < y + x
\end{coqout}

Unfortunately the binding for y is needed by the \C{odd\_y}
context item, so \C{move: y} fails.  One has to push on the stack
items in a valid order.  \C{move: y odd\_y} would push \C{odd\_y}
first, then \C{y}, leading to the valid goal

\begin{coqout}{name=Stack2}{}
 x : nat
 x_gt1 : 1 < x
 ========================
 forall y, odd y -> 2 < y + x
\end{coqout}
\index[ssr]{\C{tactic : ..}!\C{: term} (generalization)}

Via the execution of \C{case} one obtains:

\begin{coqout}{name=Stack2}{}
2 subgoals

  x : nat
  x_gt1 : 1 < x
  ========================
   odd 0 -> 2 < 0 + x

subgoal 2 is:
 forall n : nat, odd n.+1 -> 2 < n.+1 + x
\end{coqout}

An alternative to discharging \C{odd\_y} would be to clear it, i.e. purge it
from the context.  Listing context entry names inside curly braces has this
effect.  For example \C{case: y \{odd\_y\}}.
\index[ssr]{\C{tactic : ..}!\C{: \{name\}} (disposal)}

One can combine \C{:} and \C{=>} around a proof command, to first prepare the
goal for its execution and finally apply the necessary bookkeeping to the
result.  For example:

\begin{coq}{}{width=6cm}
case: y odd_y => [|y']
\end{coq}
\begin{coqout}{name=Stack2}{width=6cm}
2 subgoals

  x : nat
  x_gt1 : 1 < x
  ========================
   odd 0 -> 2 < 0 + x

subgoal 2 is:
 odd y'.+1 -> 2 < y'.+1 + x
\end{coqout}

At the left of the \C{:} operator one can also put a name for an
equation that links the term at the top of the stack before and
after the tactic execution.  \C{case E: y odd\_y => [|y']} leads to
the two sub goals:

\begin{coqout}{name=Stack2}{width=6cm}
 x, y : nat
 x_gt1 : 1 < x
 E : y = 0
============================
 odd 0 -> 2 < 0 + x
\end{coqout}
\begin{coqout}{name=Stack2}{width=6cm}
 x, y : nat
 x_gt1 : 1 < x
 y' : nat
 E : y = y'.+1
========================
 odd y'.+1 -> 2 < y'.+1 + x
\end{coqout}
\index[ssr]{\C{tactic : ..}!\C{name: term} (equation)}

Last, one can discharge the goal with any term, not
just variables occurring in it.  For example
``\C{move: (leqnn 7)}'' pushes on the stack
the additional assumption \C{(7 <= 7)}.  This
will come handy in section~\ref{sec:strongind}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\section{Inductive reasoning}\label{sec:indreason}

In chapter 1 we have seen how to build and use (call or destruct)
anonymous functions and data types.  All these
constructions have found a correspondence in the Curry-Howard correspondence.
The only missing piece is recursive programs.  For example
\C{addn} was written by recursion on its first argument, and is a
function taking in input two numbers and producing a third one.
We can write programs by recursion that take in input, among regular  data,
proofs and produce in output other proofs.  Let's look at the
induction principle for natural numbers trough the looking glasses of the
Curry-Howard isomorphism.

\begin{coq}{}{width=3.5cm}
About nat_ind.
\end{coq}
\begin{coqout}{}{width=9cm}
nat_ind : forall P : nat -> Prop,
  P 0 -> (forall n : nat, P n -> P n.+1) -> forall n : nat, P n
\end{coqout}
\C{nat\_ind} is a program that produces a proof of \C{(P n)} for any \C{n}
proviso a proof for the base case \C{(P 0)}, and a proof
of the inductive step \C{(forall n : nat, P n -> P n.+1)}.
Let's write such program by hand.

\begin{coq}{}{}
Fixpoint nat_ind (P : nat -> Prop)
  (p0 : P 0) (pS : forall n : nat, P n -> P n.+1) n : P n :=
  if n is m.+1 then
    let pm (* : P m *) := nat_ind P p0 pS m in
    pS m pm (* : P m.+1 *)
  else p0.
\end{coq}
\index[coq]{\C{nat\_ind}}

The \Coq{} system generates such program automatically
when the \C{nat} data type
is defined.  Recall that recursive functions are checked for termination:
trough the lenses of the proofs as programs correspondence this means
that the induction principle just coded is sound, i.e. based on a well
founded order relation.
\index[concept]{termination}
\index[concept]{consistency}

If non-terminating functions are not ruled out, it easy to inhabit
the \C{False} type, even if it lacks a proper constructor.

\begin{coq}{}{}
Fixpoint oops (n : nat) : False := oops n.
Check oops 3.  (* : False *)
\end{coq}
Of course \Coq{} rejects the definition of \C{oops}.  To avoid
loosing consistency, \Coq{} also enforces some restrictions on
inductive data types.  For example the declaration of \C{hidden}
is rejected.

\begin{coq}{}{}
Inductive hidden := Hide (f : hidden -> False).
Definition oops (hf : hidden) : False := let: Hide f := hf in f hf.
Check oops (Hide oops).  (* : False *)
\end{coq}
Note how \C{oops} calls itself, as in the previous example,
even if it is not a recursive function.
Such restriction, called
\emph{positivity condition}, is out of scope for this book.
The interested reader shall
refer to~\cite{Coq:manual}.
\index[concept]{positivity}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\mcbLEVEL{1}
\mcbsubsection{Strong induction}
\index[concept]{induction!strong}
\index[concept]{induction!curse of values}
\label{sec:strongind}

As an exercise we show how the \C{elim} tactic combined with the bookkeeping
operator \C{:} lets one perform, on the fly, a stronger variant of
induction called ``course of values''.

Claim: every amount of postage that is at least 12 cents
can be made from 4-cent and 5-cent stamps.  The proof in the inductive
step goes as follows.  There are obvious solutions for a postage between
12 and 15 cents, so we can assume it is at least 16 cents.  Since
the postage amount is at least 16, by using a 4-cents stamp we are back
at a postage amount that, by induction, can be obtained as claimed.\hfill$\square$

The tricky step is that we want to apply the induction hypothesis not
on $n-1$, as usual, but on $n-4$, since we know how to turn a
solution for a stamping amount problem $n$ to one for a problem of
size $n+4$ (by using a 4-cent stamp).
The induction hypothesis provided by \C{nat_ind}
is not strong enough.  However we can use the \C{:} operator
to load the goal before performing the induction.
\index[ssr]{\C{set name := term}}

\begin{coq}{}{}
Lemma stamps n : 12 <= n -> exists s4 s5, s4 * 4 + s5 * 5 = n.
Proof.
elim: n {-2}n (leqnn n) =>[|n IHn]; first by case.
do 12! [ case; first by [] ]. (* < 12c *)
case; first by exists 3, 0.   (* 12c = 3 * 4c *)
case; first by exists 2, 1.   (* 13c = 2 * 4c + 1 * 5c *)
case; first by exists 1, 2.   (* 14c = 1 * 4c + 2 * 5c *)
case; first by exists 0, 3.   (* 15c = 3 * 5c *)
move=> m'; set m := _.+1; move=> mn m11.
case: (IHn (m-4) _ isT) => [|s4 [s5 def_m4]].
  by rewrite leq_subLR (leq_trans mn) // addSnnS leq_addl.
by exists s4.+1, s5; rewrite mulSn -addnA def_m4 subnKC.
Qed.
\end{coq}

Just before the induction step the goal is the following one:

\begin{coq}{}{}
forall n m : nat,
  m <= n -> 11 < m -> exists s4 s5 : nat, s4 * 4 + s5 * 5 = m
\end{coq}
The corresponding induction hypothesis \C{IHn} is:

\begin{coq}{}{}
IHn : forall m : nat,
      m <= n ->
      11 < m -> exists s4 s5 : nat, s4 * 4 + s5 * 5 = m
\end{coq}
Such hypothesis is accessible for each \C{m} that is at least \C{12}
and smaller than \C{n}.

Loading the goal works as follows: first \C{(leqnn n)} is pushed on
the stack, then all occurrences of \C{n} but for the second one are
discharged obtaining

\begin{coq}{}{}
forall m : nat,
  m <= n -> 11 < m -> exists s4 s5 : nat, s4 * 4 + s5 * 5 = m
\end{coq}
Finally the last occurrence of \C{n} is discharged too so that induction is performed
on such quantified variable.

Lines 4, 9 and 10 deserve a few comments.  Line 4 repeats a tactic
12 times.  Line 9 uses the \C{set} proof command to name \C{m} a
term of the form \C{_.+1} where \C{_} is a wildcard. It names
\C{m} the term \C{m'.+16}.  Line 10 passes to the induction
hypothesis \C{(m-4)}, then a placeholder for a missing proof
of \C{(m-4 < n)}, finally a proof that \C{(11 < m-4)}.  The introduction
pattern in line 10 names \C{s4} and \C{s5} the quantities of
4-cents and 5-cents stamps needed to cover the amount of postage \C{(m-4)}.
\index[ssr]{\C{do n"!} (iteration)}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\mcbLEVEL{1}
\mcbsection{On the status of Axioms}
\label{sec:EM}

Not all valid reasoning principles can be represented by programs.
For example excluded middle can be proved by a program only when
the property is decidable, i.e. when we can write in \Coq{} a program
to \C{bool} that tests if the property holds or not.
Excluded middle, in its generality, can only be \emph{axiomatized},
i.e. assumed globally.

The \mcbMC{} library is axiom free.  This makes the library compatible
with as many axioms as possible.  Indeed not all combinations of well
known, consistent, axioms are consistent, so if a library picks
one it rules out another one.  Moreover the formalization style
we adopted systematically expresses all decidable properties
as boolean tests, hence one rarely notices that excluded middle
is ``missing''.

Sometimes axioms like excluded middle can be confined into ``boxes''
one can open only in contexts where they can be actually proved.
For example:

\begin{coq}{}{}
Definition classically P : Prop := forall b : bool, (P -> b) -> b.
Lemma classic_EM P : classically (decidable P).
Lemma classic_bind P Q :
  (P -> classically Q) -> classically P -> classically Q.
\end{coq}
\index[coq]{\C{classically}}
\index[coq]{\C{classically_EM}}
\index[coq]{\C{classically_bind}}
Here \C{classically} plays the role of a box, typically called \emph{monad}
in computer science, that one can only open when the goal is a boolean,
hence decidable, predicate.  Inside such box the excluded middle is
made available by combining \C{classic_EM} and \C{classic_bind}.
Nevertheless, when proving a statement that is not a boolean, like
\C{exists n, ...}, one cannot access assumptions in the \C{classically} box.

In other cases axioms can be avoided by rephrasing the mathematics
in a weaker setting.  A notable example in the \mcbMC{} library
is the construction of the real closure of an Archimedean
field~\cite{DBLP:conf/itp/Cohen12}.
%\marginnote{TODO Cyril: say more about RCF}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\mcbLEVEL{1}
\mcbsection{Terms, types, proofs: all in the same pot}

For a newcomer
a quite confusing feature of the \mcbCIC{} is that the same syntax
is shared among term, proofs, statements and data types.  In other words terms
can occur in types and types can occur in terms.  And of course terms and
types play the double role of proofs and statements respectively.
This is strictly related to the range the quantifications are allowed on,
and to the fact that they can be used to form types.
We have seen quantification on terms, like ``\C{ forall n : nat}'' and on types
like ``\C{ forall P : Prop}'' or ``\C{ forall T : Type}''.  The subtle difference
between the label \C{Prop} for propositions and \C{Type} for data types
is out of scope here, we consider them synonyms.

An excellent example of these quantification is the inductive type
modelling the existential quantification:

\begin{coq}{}{}
ex : forall A : Type, (A -> Prop) -> Prop.
ex_intro : forall A : Type, forall P : A -> Prop, forall a : A, P a -> ex A P.
\end{coq}

The \C{ex} type constructor is polymorphic in a type \C{A} and a predicate
on \C{A}.  Remark that also \C{seq} is polymorphic, but only in one argument
so the quantification can be non dependent: \C{(seq : Type -> Type)}.

In the constructor \C{ex_intro} the quantification on \C{P} also needs to be
dependent: first because it must build a term of type \C{(ex A P)} (that
mentions \C{P}); second because the last argument needs to be of a proof of
\C{P}, so its type must mention the predicate.  Again in the case of \C{seq}
the constructor \C{(cons : forall A, A -> seq A -> seq A)} needs no dependent
quantification on the head element, while here \C{a} needs to occur in
the type of the argument that follows it, since it must be a proof of
\C{(P a)}.

The \C{ forall } quantification is not only useful to form types that
play the role of propositions.  It can come handy to form data types
too.  In the second part of this book (section~\ref{sec:matrix})
we see how the matrix data type
and the type of its multiplication function benefit from it.

\begin{coq}{}{}
matrix : Type -> nat -> nat -> Type.
mulmx : forall R : Type, forall m n p : nat,
  matrix R m n -> matrix R n p -> matrix R m p.
\end{coq}

This time the data type of matrices exposes the size, and matrix multiplication
can be given a type that rules out incompatible matrices and also describes
the dimension of the resulting matrix in terms of the size of the input
ones.\footnote{In veritas, \C{mulmx} also needs to take in input operations to
add and multiply elements of \C{R}.  Such detail plays no role in the current
discussion.}
This last use of of the \C{ forall } quantifier is a classic example
of what is called a \emph{dependent type}: a data type depending on
data, two natural numbers here.
\index[concept]{dependent type}

Last, a quantification can also range on the proofs of a statement
so that one can talk about proofs.  Such capability finds a rare, but
crucial use in \mcbMC{} that we discuss in chapter~\ref{ch:sigmabool}.

\begin{coq}{name=hedberg}{}
Lemma bool_irrelevance (P Q : bool) : forall e1 e2 : P = Q, e1 = e2.
\end{coq}

%
%           nat -> nat a function, like pred
%forall x : nat, p n   a statement, n = n
%forall x : nat, T n   a rich data type constructor
%           Type -> Type  a poly data type constructor
%forall T : Type, F T     a polymorphic data type
%(bla : Prop) -> 'I_m
%
%Something possible but never? used in mcbmc is
%
%forall p : (bla : Prop), T p
%
%one may find
%
%forall i : 'I_k, T i
%
%but recall the proof part in i is irrelevant.
%
%
%
